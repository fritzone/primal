set(project compiler)

set(registered_keywords "")

function(register_keyword keyword)
    set(registered_keywords "${registered_keywords};${keyword}" PARENT_SCOPE)
endfunction()

#
# Generic project source files
#
set(${project}-sources
        ${CMAKE_CURRENT_SOURCE_DIR}/ast.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/parser.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/token.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/lexer.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/util.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/source.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/sequence.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/variable.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/generate.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/compiler.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/label.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/options.cpp
        )

set(${project}-headers
        ${CMAKE_CURRENT_SOURCE_DIR}/asm_compiler.h
        ${CMAKE_CURRENT_SOURCE_DIR}/ast.h
        ${CMAKE_CURRENT_SOURCE_DIR}/parser.h
        ${CMAKE_CURRENT_SOURCE_DIR}/token.h
        ${CMAKE_CURRENT_SOURCE_DIR}/operators.h
        ${CMAKE_CURRENT_SOURCE_DIR}/lexer.h
        ${CMAKE_CURRENT_SOURCE_DIR}/util.h
        ${CMAKE_CURRENT_SOURCE_DIR}/source.h
        ${CMAKE_CURRENT_SOURCE_DIR}/sequence.h
        ${CMAKE_CURRENT_SOURCE_DIR}/variable.h
        ${CMAKE_CURRENT_SOURCE_DIR}/generate.h
        ${CMAKE_CURRENT_SOURCE_DIR}/keywords.h
        ${CMAKE_CURRENT_SOURCE_DIR}/compiler.h
        ${CMAKE_CURRENT_SOURCE_DIR}/label.h
        ${CMAKE_CURRENT_SOURCE_DIR}/options.h
        )

########################################################################################################################
#                 Register the keywords, add yours and extend here if necessary.                                       #
#                                                                                                                      #
#                  Register the keyword by using: register_keyword(YOURKEYWORD)                                        #
#                                                                                                                      #
#   Do not forget to create a kw_YOURKEYWORD.cpp and a kw_YOURKEYWORD.h in the keywords subdirectory of the compiler.  #
#                                                                                                                      #
########################################################################################################################

register_keyword(if)
register_keyword(endif)
register_keyword(let)
register_keyword(asm)
register_keyword(goto)

########################################################################################################################
#                 Done, no more keywords have to be added after this point in the code                                 #
########################################################################################################################

# Now create the "all_keywords.h" and all_keywords.cpp
string(TIMESTAMP now)
set(KEYWH "${CMAKE_CURRENT_BINARY_DIR}/all_keywords.h")
set(KEYWCPP "${CMAKE_CURRENT_BINARY_DIR}/all_keywords.cpp")

file(WRITE ${KEYWH} "// Autogenerated by CMake on ${now}. All modifications to this file will be lost\n")
file(WRITE ${KEYWCPP} "// Autogenerated by CMake on ${now}. All modifications to this file will be lost\n")

file(APPEND ${KEYWH}  "#ifndef ALL_KEYWORDS_H_INCLUDED\n")
file(APPEND ${KEYWH} "#define ALL_KEYWORDS_H_INCLUDED\n")

file(APPEND ${KEYWCPP} "#include \"all_keywords.h\"\n")
file(APPEND ${KEYWCPP} "void register_all_keywords()\n{")


foreach(keyword ${registered_keywords})
    list(APPEND ${project}-sources ${CMAKE_CURRENT_SOURCE_DIR}/keywords/kw_${keyword}.cpp)

    file(APPEND ${KEYWH} "#include \"keywords/kw_${keyword}.h\"\n")
    file(APPEND ${KEYWCPP} "REGISTER_KEYWORD(kw_${keyword});\n")
endforeach()

include_directories(${CMAKE_CURRENT_BINARY_DIR})

file(APPEND ${KEYWH} "void register_all_keywords();\n")

file(APPEND ${KEYWH} "#endif\n")
file(APPEND ${KEYWCPP} "}\n")

add_library(${project}
        ${${project}-sources}
        ${${project}-headers}
        ${CMAKE_CURRENT_BINARY_DIR}/all_keywords.cpp
)

target_link_libraries(${project} opcode-compilers)